{
  inputs,
  username,
  homeDirectory,
  config,
  lib,
  pkgs,
  ...
}:

{
  imports = [ ../dev/home.nix ];

  nixpkgs.config = {
    users.defaultUserShell = pkgs.zsh;
  };
  targets.genericLinux = {
    enable = true;
    gpu.enable = false;
  };
  home = {
    inherit username homeDirectory;
    stateVersion = "25.05";
    sessionVariables = {
      SSH_ASKPASS = "termux-ssh-askpass";
      SSH_ASKPASS_REQUIRE = "force";
      SSH_AUTH_SOCK = "$PREFIX/var/run/ssh-agent.socket";
    };
    shellAliases =
      let
        shimLib = pkgs.callPackage ./nix-gc-proot.nix { };
        nixGc = "LD_PRELOAD=${shimLib}/lib/proc-eperm-shim.so nix-collect-garbage";
      in
      {
        nix-collect-garbage = nixGc;
        hmu = "NIXPKGS_ALLOW_UNFREE=1 home-manager switch --flake ~/.config/home-manager#${username}@android --impure && nix profile wipe-history && home-manager expire-generations '-1 second' && ${nixGc}";
      };
  };

  home.packages = with pkgs; [
    fastfetch
    iconv # needed for zsh
  ];

  home.file = {
    "termux-ssh-askpass" = {
      source = ./termux-ssh-askpass;
      target = ".local/bin/termux-ssh-askpass";
      executable = true;
    };
    # taken from nix-on-droid stylix integration
    # it wont work as intended bc you log in to vanilla termux, and this is symlink to prooted nix path
    # therefore you should manually create regular file without .lnk after each change
    ".termux/colors.properties.lnk".text = lib.concatStringsSep "\n" [
      "background = #${config.lib.stylix.colors.base00}"
      "foreground = #${config.lib.stylix.colors.base05}"
      "cursor = #${config.lib.stylix.colors.base05}"
      "# normal"
      "color0 = #${config.lib.stylix.colors.base00}"
      "color1 = #${config.lib.stylix.colors.base08}"
      "color2 = #${config.lib.stylix.colors.base0B}"
      "color3 = #${config.lib.stylix.colors.base0A}"
      "color4 = #${config.lib.stylix.colors.base0D}"
      "color5 = #${config.lib.stylix.colors.base0E}"
      "color6 = #${config.lib.stylix.colors.base0C}"
      "color7 = #${config.lib.stylix.colors.base05}"
      "# bright"
      "color8 = #${config.lib.stylix.colors.base02}"
      "color9 = #${config.lib.stylix.colors.base08}"
      "color10 = #${config.lib.stylix.colors.base0B}"
      "color11 = #${config.lib.stylix.colors.base0A}"
      "color12 = #${config.lib.stylix.colors.base0D}"
      "color13 = #${config.lib.stylix.colors.base0E}"
      "color14 = #${config.lib.stylix.colors.base0C}"
      "color15 = #${config.lib.stylix.colors.base07}"
    ];
  };

  programs = {
    home-manager.enable = true;
    zsh = {
      initContent =
        let
          localBin = lib.mkOrder 1500 ''export PATH="$HOME/.local/bin:$PATH"'';
        in
        lib.mkMerge [ localBin ];
    };
  };

  gtk.enable = false;
  systemd.user.enable = false;
  qt.enable = false;
  xdg.portal.enable = false;

  news.display = "silent";

  stylix = {
    enable = true;
    autoEnable = false;
    targets = {
      fzf.enable = true;
      gitui.enable = true;
      yazi.enable = true;
      zellij.enable = true;
    };
    base16Scheme = "${pkgs.base16-schemes}/share/themes/da-one-ocean.yaml";
    polarity = "dark";
  };
}
